use System.Concurrency;
use Collection.Generic;
use Web.HTTP.Server;

#~
Mini web server
~#
class Test {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 4) {
			cert := args[0];
			cert_key := args[1];
			cert_key_passwd := args[2];
			port := args[3]->ToInt();

			RequestHandler->Init();			
			WebServer->ServeSecure(RequestHandler->New()->GetClass(), port, cert, cert_key, cert_key_passwd, true);
		};
	}
}

#~
Request handler
~#
class RequestHandler from HttpsRequestHandler {
	@content_cache : static : Cache<String, ByteArrayHolder>;
	@content_type_cache : static : Hash<String, String>;
	@content_mutex : static : ThreadMutex;
	
	function : Init() ~ Nil {
		# MIME type mapping
		@content_type_cache := Hash->New()<String, String>;
		@content_type_cache->Insert(".html", "text/html");
		@content_type_cache->Insert(".htm", "text/html");
		@content_type_cache->Insert(".css", "text/css");
		@content_type_cache->Insert(".png", "image/png");
		@content_type_cache->Insert(".jpg", "image/jpeg");
		@content_type_cache->Insert(".ico", "image/x-icon");
		@content_type_cache->Insert(".js", "application/javascript");

		@content_cache := Cache->New(Cache->Type->MRU, 16)<String, ByteArrayHolder>;
		@content_mutex := ThreadMutex->New("__content_cache__");
	}

	New() {
		Parent();
	}

	method : ProcessGet(request : Request, response : Response) ~ Nil {
		request_url := request->GetUrl();

		content : Byte[];
		request_type_index := request_url->FindLast('.');
		if(request_type_index > 0) {
			# check the request type
			request_type := request_url->SubString(request_type_index, request_url->Size() - request_type_index);
			if(request_type <> Nil) {
				content_type := @content_type_cache->Find(request_type);
				if(content_type <> Nil) {
					path_name := "./html";
					path_name += request_url;

					# lock cache for while we search or insert content
					critical(@content_mutex) {
						# found in cache
						found := @content_cache->Find(path_name);
						if(found <> Nil) {
							content := found->Get();
						}
						# not found, add to cache
						else {
							content := System.IO.Filesystem.FileReader->ReadBinaryFile(path_name);
							if(content <> Nil) {
								@content_cache->Insert(path_name, ByteArrayHolder->New(content));
							};
						};
					};

					if(content <> Nil) {
						response->SetHeader("Content-Type", "{$content_type}");
					};
				};
			};
		};

		if(content <> Nil) {
			# just a test... likely store compressed assets
			encoding_types := request->GetRequestHeader("Accept-Encoding");
			if(encoding_types <> Nil & encoding_types->Has("gzip")) {
				response->SetHeader("Content-Encoding", "gzip");
				content := content->CompressGzip();
			};
			
			response->SetCodeContent(200, content);
		}
		else {
			response->SetCode(404);
		};
	}

	method : ProcessPost(request : Request, response : Response) ~ Nil {
		buffer := request->GetContentBytes();
		if(buffer <> Nil & buffer->Size() > 0) {
			decoder := MultipartEncoding->New(buffer);
			contents := decoder->Parse()<MultipartContent>;
			if(contents->Size() = 1) {
				content := contents->Get(0);
				header_names :=  content->GetHeaderNames()<String>;
				each(j : header_names) {
					header_names->Get(j)->PrintLine();
				};
				System.IO.Filesystem.FileWriter->WriteFile("html/upload.jpg", content->GetContent());
			};
		};

		response->SetCodeContent(200, "<html><h2>Hello World!</h2><br/><img src='upload.jpg'>Uploaded Image</img><html>");
	}
}