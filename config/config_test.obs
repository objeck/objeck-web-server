use Collection.Generic;
use Data.JSON;

class WebServerConfig {
	@filename : String;
	@instance_str : String;
	@is_debug : Bool;
	@host : String;
	@port : Int;
	@cert_path : String;
	@cert_key : String;
	@cert_passwd : String;
	@file_type_handler_map : Hash<String, FileTypeHandler>;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			config := WebServerConfig->New(args[0]);
			if(config->Load()) {
				config->ToString()->PrintLine();
			};
		};
	}

	New(filename : String) {
		@filename := filename;
		@file_type_handler_map := Hash->New()<String, FileTypeHandler>;
	}

	method : public : GetFilename() ~ String {
		return @filename;
	}

	method : public : GetInstance() ~ Class {
		class_instance := Class->Instance(@instance_str);
		if(class_instance <> Nil) {
			class_instance->GetClass();
		};

		return Nil;
	}
	
	method : public : IsDebug() ~ Bool {
		return @is_debug;
	}

	method : public : GetHost() ~ String {
		return @host;
	}

	method : public : GetPort() ~ Int {
		return @port;
	}

	method : public : GetCertPath() ~ String {
		return @cert_path;
	}

	method : public : GetCertKey() ~ String {
		return @cert_key;
	}

	method : public : Load() ~ Bool {
		parser := JsonParser->New(System.IO.Filesystem.FileReader->ReadFile(@filename));
		if(parser->Parse()) {
			# process network config
			network_json := parser->GetRoot()->Get("network");
			if(network_json = Nil) {
				return false;
			};
			
			network_instance_json := network_json->Get("instance");
			network_host_json := network_json->Get("host");
			network_port_json := network_json->Get("port");
			if(network_instance_json = Nil | network_host_json = Nil | network_port_json = Nil) {
				return false;
			};
			@instance_str := network_instance_json->GetString();
			@host := network_host_json->GetString();
			@port := network_port_json->GetString()->ToInt();

			is_debug_json := network_json->Get("debug");
			if(is_debug_json <> Nil) {
				@is_debug := is_debug_json->GetBool();
			};
			
			network_secure_json := network_json->Get("secure");
			if(network_secure_json <> Nil) {
				network_cert_path_json := network_secure_json->Get("cert_path");
				network_cert_key_json := network_secure_json->Get("cert_key_path");
				network_cert_passwd_json := network_secure_json->Get("cert_passwd");

				if(network_cert_path_json = Nil | network_cert_key_json = Nil | network_cert_passwd_json = Nil) {
					return false;
				};
				@cert_path := network_cert_path_json->GetString();
				@cert_key := network_cert_key_json->GetString();
				@cert_passwd := network_cert_passwd_json->GetString();
			};

			# process files
			files_json := parser->GetRoot()->Get("files");
			if(files_json = Nil) {
				return false;
			};

			each(i : files_json) {
				file_json := files_json->Get(i);
				if(file_json->Has("types")) {
					if(<>AddFiles(file_json)) {
						return false;
					};
				};
			};

			return true;
		};

		return false;
	}

	method : AddFiles(file_json : JsonElement) ~ Bool {
		types_json := file_json->Get("types");
		location_json :=  file_json->Get("location");

		if(types_json <> Nil & location_json <> Nil) {
			types_str := types_json->GetString();
			type_strs := types_str->Split(',');
			if(type_strs->Size() > 0) {
				each(i : type_strs) {
					if(<>AddHandler(type_strs[i], file_json)) {
						return false;
					};
				};
			}
			else {
				if(<>AddHandler(types_str, file_json)) {
					return false;
				};
			};
		};

		return true;
	}

	method : AddHandler(type : String, file_json : JsonElement) ~ Bool {
		alias_str : String;
		alias_json := file_json->Get("alias");
		if(alias_json <> Nil) {
			alias_str := alias_json->GetString();
		};

		location_str : String;
		location_json := file_json->Get("location");
		if(location_json <> Nil) {
			location_str := location_json->GetString();
		};

		cache_flag : Bool;
		cache_json := file_json->Get("cache");
		if(cache_json <> Nil) {
			cache_flag := cache_json->GetBool();
		};

		# get/add handler
		type_handler := @file_type_handler_map->Find(type);
		if(type_handler = Nil) {
			type_handler := FileTypeHandler->New(type);
			@file_type_handler_map->Insert(type, type_handler);
		};

		if(<>type_handler->Insert(alias_str, location_str, cache_flag)) {
			"Unable to add file, duplicate location '{$location_str}'"->ErrorLine();
			return false;
		};

		return true;
	}

	method : public : ToString() ~ String {
		buffer := "[Network]\n";
		buffer += "\tfilename='{$@filename}', instance='{$@instance_str}', host='{$@host}', port={$@port}, is_debug={$@is_debug}\n";
		if(@cert_path <> Nil & @cert_key <> Nil & @cert_passwd <> Nil) {
			buffer += "\tcert_path='{$@cert_path}', cert_key='{$@cert_key}', cert_passwd='{$@cert_passwd}'\n";
		};

		buffer += "[Files]\n";
		file_type_pairs := @file_type_handler_map->GetKeyValues()<Pair<String, FileTypeHandler>>;
		each(i : file_type_pairs) {
			file_type_pair := file_type_pairs->Get(i);

			file_type_name := file_type_pair->GetFirst();
			buffer += "\t{{$file_type_name}}\n";
			file_type_handler := file_type_pair->GetSecond();
			buffer += file_type_handler->ToString();
		};
		
		return buffer;
	}
}

class : private : FileTypeHandler {
	@type : String;
	@alias_map : Map<String, FileHandler>;
	@location_map : Map<String, FileHandler>;

	New(type : String) {
		@type := type;
		@alias_map := Map->New()<String, FileHandler>;
		@location_map := Map->New()<String, FileHandler>;
	}

	method : public : Insert(alias_str : String, location_str : String, cache_flag : Bool) ~ Bool {
		if(@location_map->Has(location_str)) {
			return false;
		};

		file_handler := FileHandler->New(@type, alias_str, location_str, cache_flag);
		@location_map->Insert(location_str, file_handler);
		if(alias_str <> Nil) {
			@alias_map->Insert(alias_str, file_handler);
		};

		return true;
	}

	method : public : ToString() ~ String {
		buffer := "";

		file_handlers := @location_map->GetValues()<FileHandler>;
		each(i : file_handlers) {
			file_handler := file_handlers->Get(i);
			buffer += "\t\t";
			buffer += file_handler->ToString();
			buffer += "\n";
		};

		return buffer;
	}
}

class : private : FileHandler {
	@type : String;
	@alias_str : String;
	@location : String;
	@cache : Bool;

	New(type : String, alias_str : String, location : String, cache : Bool) {
		@type := type;
		@alias_str := alias_str;
		@location := location;
		@cache := cache;
	}

	method : public : GetAlias() ~ String {
		return @alias_str;
	}

	method : public : GetLocation() ~ String {
		return @location;
	}

	method : public : GetCache() ~ Bool {
		return @cache;
	}

	method : public : ToString() ~ String {
		if(@alias_str <> Nil) {
			return "alias='{$@alias_str}', location='{$@location}', cache='{$@cache}', type='{$@type}'";
		};

		return "location='{$@location}', cache='{$@cache}', type='{$@type}'";
	}
}
